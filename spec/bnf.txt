stmts -> stmt morestmts.
morestmts -> stmts
|.
stmt -> innerstmt endstmt.
endstmt -> ;.
innerstmt -> decl
| expr
| if cond block elseif endif
| while cond optanchor block
| return expr
| specialid ident { decl endblock
| break anchor
| continue anchor.
optanchor -> : anchor
|.
anchor -> anchorname
|.
block -> { morestmts endblock.
endblock -> }.
decl -> type moredecl anotherdecl.
moredecl -> ptr declname funcdel enddecl.
ptr -> * ptr
|.
funcdel -> nocallfunc openparen args endargs fwddecl
|.
nocallfunc -> $
|.
args -> decl
|.
fwddecl -> block
|.
endargs -> ).
declname -> ident.
enddecl -> arr assign
|.
assign -> = expr.
arr -> [ const morearr.
morearr -> , const morearr
| ].
anotherdecl -> , decl
|.
cond -> ( expr endcond.
endcond -> ).
elseif -> elif cond block elseif
|.
endif -> else block
|.
expr -> unop basename
| basename morexpr
| ( expr moreparens.
moreparens -> ).
basename-> name expr'.
morexpr -> binop expr
|.
name -> ident
| const.
expr' -> arrexpr expr'
| funcall expr'
| structcall expr'
|.
funcall -> ( expr morecall.
morecall -> , expr morecall
| ).
structcall -> STRUCTPOINT ident
| STRUCTDEREF ident.
arrexpr -> [ expr endarr.
endarr -> , expr endarr
| ].
unop -> &
| *
| !
| ~
| cast.
cast -> casttype morecast.
casttype -> s
| 0
| 1.
morecast -> openparen endcast.
openparen -> (.
endcast -> type ptr moreparens.
binop -> -
| +
| /
| *
| OR
| AND
| XOR
| BOR
| BAND
| BXOR
| >>
| <<
| ))
| ((
| ==
| >
| >=
| <
| <=.